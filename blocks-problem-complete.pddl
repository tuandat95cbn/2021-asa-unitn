;; domain file: blocksworld-domain-complete.pddl
(define (domain blocksworld)
  (:requirements :strips)
  (:predicates    (clear ?x)
		  (on-table ?x)
		  (holding ?x)
		  (on ?x ?y)
		  (free)
		  )
  (:action pickup
	   :parameters (?ob)
	   :precondition (and (clear ?ob) (on-table ?ob) (free))
	   :effect (and (holding ?ob)
			(not (clear ?ob))
			(not (on-table ?ob))
			(not (free))
			)
	   )
  (:action putdown
	   :parameters (?ob)
	   :precondition (holding ?ob)
	   :effect (and (not (holding ?ob))
			(clear ?ob)
			(on-table ?ob)
			(free)
			)
	   )
  (:action unstack
	   :parameters (?ob ?y)
	   :precondition (and (on ?ob ?y) (clear ?ob) (free))
	   :effect (and (not (on ?ob ?y))
			(not (clear ?ob))
			(clear ?y)
			(holding ?ob)
			(not (free))
			)
	   )
  (:action stack
	   :parameters (?ob ?y)
	   :precondition (and (holding ?ob) (clear ?y))
	   :effect (and (on ?ob ?y)
			(clear ?ob)
			(not (clear ?y))
			(not (holding ?ob))
			(free)
			)
	   )
  )

